#!/usr/bin/env stap

global flow

probe   process(@SVNSERVE_BIN).function("serve_interruptable")
{
        printf("%s -> %s\n", thread_indent(4), ppfunc())
        flow[tid()] = 1
}

probe   process(@SVNSERVE_BIN).function("serve_interruptable").return
{
        if (flow[tid()]) {
                flow[tid()] = 0
        }
}


probe   process(@SVNSERVE_BIN).function("*").call
,       process(@SVNSERVE_BIN).library(@LIBSVN_WC).function("*").call
,       process(@SVNSERVE_BIN).library(@LIBAPR).function("apr_file_*").call
,       process(@SVNSERVE_BIN).library(@LIBAPR).function("apr_stat").call
,       process(@SVNSERVE_BIN).library(@LIBAPR).function("apr_dir_*").call
,       process(@SVNSERVE_BIN).library(@LIBAPR).function("apr_socket_*").call
#,      process(@SVNSERVE_BIN).library(@LIBSVN_SUBR).function("svn_sqlite_*").call
,       process(@SVNSERVE_BIN).library(@LIBSVN_SUBR).function("svn_io_*").call
,       process(@SVNSERVE_BIN).library(@LIBSVN_CLIENT).function("svn_*").call
,       process(@SVNSERVE_BIN).library(@LIBSVN_CLIENT).function("file_*").call
,       process(@SVNSERVE_BIN).library(@LIBSVN_RA_SERF).function("*_path").call
,       process(@SVNSERVE_BIN).library(@LIBSVN_RA_SERF).function("*_report").call
,       process(@SVNSERVE_BIN).library(@LIBSVN_FS_FS).function("*").call
,       process(@SVNSERVE_BIN).library(@LIBSVN_DELTA).function("*").call
{
        if (flow[tid()]) {
                printf("%s -> %s\n", thread_indent(4), ppfunc())
        }
}

probe   process(@SVNSERVE_BIN).function("*").return
,       process(@SVNSERVE_BIN).library(@LIBSVN_WC).function("*").return
,       process(@SVNSERVE_BIN).library(@LIBAPR).function("apr_file_*").return
,       process(@SVNSERVE_BIN).library(@LIBAPR).function("apr_stat").return
,       process(@SVNSERVE_BIN).library(@LIBAPR).function("apr_dir_*").return
,       process(@SVNSERVE_BIN).library(@LIBAPR).function("apr_socket_*").return
#,      process(@SVNSERVE_BIN).library(@LIBSVN_SUBR).function("svn_sqlite_*").return
,       process(@SVNSERVE_BIN).library(@LIBSVN_SUBR).function("svn_io_*").return
,       process(@SVNSERVE_BIN).library(@LIBSVN_CLIENT).function("svn_*").return
,       process(@SVNSERVE_BIN).library(@LIBSVN_CLIENT).function("file_*").return
,       process(@SVNSERVE_BIN).library(@LIBSVN_RA_SERF).function("*_path").return
,       process(@SVNSERVE_BIN).library(@LIBSVN_RA_SERF).function("*_report").return
,       process(@SVNSERVE_BIN).library(@LIBSVN_FS_FS).function("*").return
,       process(@SVNSERVE_BIN).library(@LIBSVN_DELTA).function("*").return
{
        if (flow[tid()]) {
                printf("%s <- %s\n", thread_indent(-4), ppfunc())
        }
}

probe   process(@SVNSERVE_BIN).library(@LIBSVN_SUBR).function("prepare_statement")
{
        if (flow[tid()])
                printf("%s:\t%s\n", ppfunc(), user_string($text))
}

probe   process(@SVNSERVE_BIN).library(@LIBSVN_SUBR).function("svn_sqlite__column_revnum").return
{
        if (flow[tid()])
                printf("%s:\t%d(%d)\n", ppfunc(), $return, @entry($column))
}

probe   process(@SVNSERVE_BIN).library(@LIBSVN_SUBR).function("svn_sqlite__column_text").return
{
        if (flow[tid()])
                printf("%s:\t%s(%d)\n", ppfunc(), user_string($return), @entry($column))
}

probe   process(@SVNSERVE_BIN).library(@LIBSVN_SUBR).function("svn_sqlite__column_int*").return
{
        if (flow[tid()])
                printf("%s:\t%d(%d)\n", ppfunc(), $return, @entry($column))
}

probe   process(@SVNSERVE_BIN).library(@LIBSVN_SUBR).function("svn_sqlite__bind_int*")
{
        if (flow[tid()])
                printf("%s:\t%d\n", ppfunc(), $val)
}

probe   process(@SVNSERVE_BIN).library(@LIBSVN_SUBR).function("svn_sqlite__bind_text").return
{
        if (flow[tid()])
                printf("%s:\t%s\n", ppfunc(), user_string(@entry($val)))
}

probe   process(@SVNSERVE_BIN).library(@LIBSVN_SUBR).function("exec_sql2")
{
        if (flow[tid()])
                printf("%s:\t%s\n", ppfunc(), ($sql ? user_string($sql) : ""))
}

probe   process(@SVNSERVE_BIN).library(@LIBSVN_SUBR).function("svn_relpath_join").return
{
        if (flow[tid()])
                printf("%s:\t%s\n", ppfunc(), user_string($return))
}

probe   process(@SVNSERVE_BIN).library(@LIBSVN_SUBR).function("svn_io_check_path")
{
        if (flow[tid()])
                printf("%s:\t%s\n", ppfunc(), user_string($path))
}

probe   process(@SVNSERVE_BIN).library(@LIBSVN_SUBR).function("svn_cmdline_fputs")
{
        if (flow[tid()])
                printf("%s:\t%s", ppfunc(), user_string($string))
}

/*
probe   process(@SVNSERVE_BIN).library(@LIBSVN_SUBR).function("svn_stream_read_full").return
{
        if (flow[tid()])
                printf("%s:\t%s\n", ppfunc(), user_string($buffer))
}

probe   process(@SVNSERVE_BIN).library(@LIBSVN_SUBR).function("map_apr_finfo_to_node_kind").return
{
        if (flow[tid()])
                printf("%s:\t%s\t%d %d\n", ppfunc(), user_string(@entry($finfo->name)), user_int($kind), user_int($is_special))
}

probe   process(@SVNSERVE_BIN).library(@LIBSVN_RA_SERF).function("svn_ra_svn__write_cmd_set_path")
{
        if (flow[tid()])
                printf("%s:\t%s\n", ppfunc(), user_string($path))
}
*/

probe   process(@SVNSERVE_BIN).library(@LIBSVN_FS_FS).function("get_contents_from_windows").return
{
        if (flow[tid()])
                printf("%s:\t%s\n", ppfunc(), user_string_n(@entry($buf), @entry(user_ulong($len))))
}

probe   process(@SVNSERVE_BIN).library(@LIBSVN_WC).function("report_revisions_and_depths")
{
        if (flow[tid()]) {
                printf("%s:\t%s  %s  %d  %d\n", ppfunc(), user_string($dir_abspath),
                                                user_string($report_relpath),
                                                $dir_rev,
                                                $restore_files)
        }
}

probe   process(@SVNSERVE_BIN).library(@LIBSVN_WC).function("change_dir_prop")
{
        if (flow[tid()])
                printf("%s:\t%s=%s\n", ppfunc(), user_string($name), user_string($value->data))
}

probe   process(@SVNSERVE_BIN).library(@LIBSVN_WC).function("change_file_prop")
{
        if (flow[tid()])
                printf("%s:\t%s=%s\n", ppfunc(), user_string($name), user_string($value->data))
}


probe   process(@SVNSERVE_BIN).library(@LIBAPR).function("apr_dir_make")
{
        if (flow[tid()])
                printf("%s:\t%s\n", ppfunc(), user_string($path))
}

probe   process(@SVNSERVE_BIN).library(@LIBAPR).function("apr_dir_open")
{
        if (flow[tid()])
                printf("%s:\t%s\n", ppfunc(), user_string($dirname))
}

probe   process(@SVNSERVE_BIN).library(@LIBAPR).function("apr_dir_read")
{
        if (flow[tid()])
                printf("%s:\t%s\n", ppfunc(),($finfo->name ? user_string($finfo->name) : ""))
}

probe   process(@SVNSERVE_BIN).library(@LIBAPR).function("apr_file_open")
{
        if (flow[tid()])
                printf("%s:\t%s\n", ppfunc(), user_string($fname))
}

probe   process(@SVNSERVE_BIN).library(@LIBAPR).function("apr_stat")
{
        if (flow[tid()])
                printf("%s:\t%s\n", ppfunc(), user_string($fname))
}

probe   process(@SVNSERVE_BIN).library(@LIBAPR).function("apr_file_read").return
,       process(@SVNSERVE_BIN).library(@LIBAPR).function("apr_file_write").return
{
        if (flow[tid()])
                printf("%s:\t%s\t%s\n", ppfunc(), user_string(@entry($thefile->fname)), user_string_n(@entry($buf), user_ulong(@entry($nbytes))))
}

probe   process(@SVNSERVE_BIN).library(@LIBAPR).function("apr_file_rename")
{
        if (flow[tid()])
                printf("%s:\t%s -> %s\n", ppfunc(), user_string($from_path), user_string($to_path))
}

probe   process(@SVNSERVE_BIN).library(@LIBAPR).function("apr_file_seek").return
{
        if (flow[tid()])
                printf("%s:\t%s(%d, %d)\n", ppfunc(), user_string(@entry($thefile->fname)), @entry($where), user_ulong(@entry($offset)))
}

probe   process(@SVNSERVE_BIN).library(@LIBAPR).function("apr_socket_send").return
,       process(@SVNSERVE_BIN).library(@LIBAPR).function("apr_socket_recv").return
{
        if (flow[tid()])
                printf("%s:\t%d\t%s\n", ppfunc(), user_ulong(@entry($len)), user_string_n(@entry($buf), user_ulong(@entry($len))))
}
